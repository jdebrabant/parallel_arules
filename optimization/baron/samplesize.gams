* GAMS model for the computation of the optimal number of samples N and single
* sample error probabilty phi.
* The optimization problem is a MNLP (Mixed Integer Non Linear Program).
* It can be solved using BARON or any global MINLP solver.
* BARON can be used through the NEOS server web interface at
* http://www.neos-server.org/neos/solvers/minco:BARON/GAMS.html

option minlp=baron;

SCALAR delta global confidence /0.01/ ;
SCALAR epsilon accuracy /0.05/ ;
SCALAR d VC-dimension /28/ ;
SCALAR m local sample size limit /25000/ ;
SCALAR W global sample size limit /5000000/ ;

VARIABLES
	objval	global sample size 
	N 	number of samples
	phi	local confidence ;

FREE VARIABLE objval ;
INTEGER VARIABLE N ;
POSITIVE VARIABLE phi ;

* We want at least one sample.
N.lo	= 1 ;

* phi must be >= delta, and such that the local sample size is less than m. phi
* must also be < 0.5, otherwise N(1-phi) < N/2, which cannot happen.
phi.lo	= MAX(delta, EXP(-(m*(epsilon**2))/2 +d)) ;
phi.up	= 0.5;

EQUATIONS
	objfun		objective function (global sample size)
	maxSampleSize	constraint on maximum sample size
	Rconstr		constraint on number of votes;

	objfun .. 	objval =e= N*(2/(epsilon**2))*(d+LOG(1/phi)) ;
	maxSampleSize ..N*(2/(epsilon**2))*(d+LOG(1/phi)) =l= W ;
	Rconstr ..	N*(1-phi)-SQRT(N*(1-phi)*2*LOG(1/delta)) =g= N /2 + 1;

MODEL samplesize /ALL/;

	SOLVE samplesize USING MINLP MINIMIZING objval ;

DISPLAY objval.l, N.l, phi.l ;

