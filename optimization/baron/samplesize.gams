* GAMS model for the computation of the optimal number of samples N and single
* sample error probabilty parameter \phi.
* The optimization problem is a MNLP (Mixed Integer Non Linear Program).
* To be solved using BARON or any global MNLP solver.
* BARON can be used through the NEOS server web interface at
* http://www.neos-server.org/neos/solvers/minco:BARON/GAMS.html

SCALAR delta global confidence /0.1/ ;
SCALAR epsilon accuracy /0.02/ ;
SCALAR d VC-dimension /2/ ;
SCALAR w local size limit /16000/ ;
SCALAR M global size limit /4000000/ ;

VARIABLES
	objval	global sample size 
	N 	number of samples
	phi	local confidence ;

FREE VARIABLE objval ;
INTEGER VARIABLE N ;
POSITIVE VARIABLE phi ;

N.lo	= 1 ;

* phi must be >= delta, and such that the sample size is less than w. phi must
* also be < 0.5, or N(1-phi) < N/2, which cannot happen.
phi.lo	= MAX(delta, EXP(-(w*(epsilon**2))/2 +d)) ;
phi.up	= 0.5;

EQUATIONS
	objfun		objective function (global sample size)
	maxSampleSize	constraint on maximum sample size
	Rconstr		constraint on number of votes;

	objfun .. 	objval =e= N*(2/(epsilon**2))*(d+LOG(1/phi)) ;
	maxSampleSize ..N*(2/(epsilon**2))*(d+LOG(1/phi)) =l= M;
	Rconstr ..	N*(1-phi)-SQRT(N*(1-phi)*2*LOG(1/delta)) =g= N /2 + 1;

MODEL samplesize /ALL/;

	SOLVE samplesize USING MINLP MINIMIZING objval ;

DISPLAY objval.l, N.l, phi.l ;

